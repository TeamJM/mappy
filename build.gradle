buildscript {
    repositories {
        maven {
            name = 'KotDis'
            url = 'https://maven.kotlindiscord.com/repository/maven-public/'
        }
    }
}

plugins {
    id 'application'

    id 'com.github.jakemarsden.git-hooks' version '0.0.1'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'io.gitlab.arturbosch.detekt' version '1.13.1'
    id 'org.jetbrains.kotlin.jvm' version '1.5.30'
}

group 'info.journeymap.mappy'
version '3.0-SNAPSHOT'

sourceCompatibility = 1.14

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        name = 'KotDis'
        url = 'https://maven.kotlindiscord.com/repository/maven-public/'
    }

    maven {
        name = "Fabric"
        url = uri("https://maven.fabricmc.net")
    }

    maven {
        name = "QuiltMC (Releases)"
        url = uri("https://maven.quiltmc.org/repository/release/")
    }

    maven {
        name = "QuiltMC (Snapshots)"
        url = uri("https://maven.quiltmc.org/repository/snapshot/")
    }

    maven {
        name = "Shedaniel"
        url = uri("https://maven.shedaniel.me")
    }

    maven {
        name = 'Jitpack'
        url = 'https://jitpack.io/'
    }
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.13.1"

    implementation 'com.kotlindiscord.kord.extensions:kord-extensions:1.5.1-SNAPSHOT'
    implementation 'com.kotlindiscord.kord.extensions:extra-mappings:1.5.1-SNAPSHOT'
    implementation 'com.kotlindiscord.kord.extensions:extra-phishing:1.5.1-SNAPSHOT'

    implementation 'com.uchuhimo:konf:0.23.0'
    implementation 'com.uchuhimo:konf-toml:0.23.0'
    implementation 'com.uchuhimo:konf-yaml:0.22.1'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'io.ktor:ktor-client-logging:1.6.0'

    implementation 'org.nibor.autolink:autolink:0.10.0'

    // Logging dependencies
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'io.github.microutils:kotlin-logging:2.0.3'
    implementation 'org.codehaus.groovy:groovy:3.0.8'  // For logback config
}

application {
    mainClassName = "info.journeymap.mappy.MainKt"
}

compileKotlin {
    kotlinOptions.jvmTarget = '14'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '14'
}

detekt {
    buildUponDefaultConfig = true
    config = files("detekt.yml")
}

gitHooks {
    hooks = ['pre-commit': 'detekt']
}

jar {
    manifest {
        attributes(
                "Main-Class": "info.journeymap.mappy.MainKt"
        )
    }
}

processResources {
    dependsOn "generateBuildInfo"

    from "src/main/resources/build.properties"
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

task generateBuildInfo() {
    outputs.files "src/main/resources/build.properties"

    doLast {
        new File("src/main/resources/build.properties").text = """
            version=$project.version
        """.stripIndent().strip()
    }
}
